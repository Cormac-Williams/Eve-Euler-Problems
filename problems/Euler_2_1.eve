# Euler Problem 2
Even Fibonacci numbers
Problem 2
Each new term in the Fibonacci sequence is generated by adding the previous two terms. By starting with 1 and 2, the first 10 terms will be:

1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...

By considering the terms in the Fibonacci sequence whose values do not exceed four million, find the sum of the even-valued terms.

### Set Up Globals.
Count : The number of steps taken.
last-count : Holds the previous start value of count for a calculation run.
value : Accumulator for the sum so far. Holds the answer at the end. Starts at 2 because fib-prev-2 is 2.
fib-prev-2
fib-prev-1 - 1st and 2nd previous fibonacci numbers.
frames : Last frame value when the calculation manager runs.
```
commit
[#count count:1 fib-prev-2:1 fib-prev-1:2 last-count:0 value:2 frames:0]
```

Calculate the current fibonacci number.
```
search
  cnt = [#count ]
bind
  cnt.fib-current := cnt.fib-prev-1 + cnt.fib-prev-2
```

### Do the Calculation.
There are a couple of variations that would work in the layout of the code. But a range is not a good answer, it leads you into infinite loops too easily.
Stop calculating if count = 1000 or the count of this run = 290. The run will be restarted by another block. This avoids fixpoint problems.
```eve
search
  cnt = [#count fib-prev-2 fib-prev-1 fib-current]
 
  //The below ensures that fib-current has been calced before running. 
  fib-current > fib-prev-1
  fib-current <= 4000000
  cnt.count - cnt.last-count < 290

  a = mod[value:fib-current by:2]
  
  diff = if is(a = 0) = true then
           fib-current
         else
           0
commit
  cnt.count := cnt.count + 1
  cnt.fib-prev-2 := fib-prev-1
  cnt.fib-prev-1 := fib-current
  cnt.fib-current := none
  cnt.value := cnt.value + diff
```

### Manage the Calculation.
If the calculation is turned off and frames is different from last run then reset the calculation.
In this calculation it probably does not get used.
```eve
search
[#time frames]
cnt = [#count]
cnt.frames != frames
cnt.count - cnt.last-count = 290
commit
  cnt.last-count := cnt.count
  cnt.frames := frames
```

### Output the Answer.
```eve
search
  cnt = [#count]
  current = cnt.fib-current
bind @view
  [#value value:"Answer = {{cnt.value}}"]
  

```
